plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.5'
}
ext {
    springBootVersion = '3.5.6'
    springDependencyManagementVersion = '1.1.7'
    protobufPluginVersion = '0.9.5'
    grpcSpringBootStarterVersion = '3.1.0.RELEASE'
    grpcVersion = '1.76.0'
    commonsIoVersion = '2.20.0'
    jacksonUuidVersion = '5.1.1'
    protocVersion = '4.33.0'
}
group = 'io.opentelemetry'
version = '0.0.1-SNAPSHOT'
description = 'OpenTelemetry Opamp gateway'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'


    // spring boot starter grpc
    implementation 'net.devh:grpc-spring-boot-starter:' + grpcSpringBootStarterVersion
    implementation 'io.grpc:grpc-netty-shaded:' + grpcVersion
    implementation 'io.grpc:grpc-protobuf:' + grpcVersion
    implementation 'io.grpc:grpc-stub:' + grpcVersion

    // apache-common-io
    implementation 'commons-io:commons-io:' + commonsIoVersion

    // jackson
    implementation 'com.fasterxml.uuid:java-uuid-generator:' + jacksonUuidVersion

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'com.mysql:mysql-connector-j'
    // H2 Database (runtime)
//    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'com.h2database:h2'

    // Protobuf
    implementation 'com.google.protobuf:protobuf-java:' + protocVersion // 최신 버전으로 업데이트

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // slf4j
    implementation 'org.slf4j:slf4j-api'
    implementation 'ch.qos.logback:logback-classic'

    // Test Dependencies
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    main {

        proto {
            srcDirs += 'src/main/proto'
        }

    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:' + protocVersion // 최신 버전으로 업데이트
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:' + grpcVersion
        }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.builtins {
                java {
                }
            }
            task.plugins {
                grpc {
                }
            }
        }
    }
}

// generatedProto before build
tasks.named('compileJava') {
    dependsOn tasks.named('generateProto')
}


tasks.named('test') {
    useJUnitPlatform()
}


tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
